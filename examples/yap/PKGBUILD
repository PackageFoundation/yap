pkgname=yap
pkgver=1.0
pkgrel=1
pkgdesc="Package software with ease"
pkgdesc__alpine="Package software with ease for Alpine"
pkgdesc__arch="Package software with ease for Arch"
pkgdesc__centos="Package software with ease for CentOS"
pkgdesc__debian="Package software with ease for Debian"
pkgdesc__fedora="Package software with ease for Fedora"
pkgdesc__ubuntu="Package software with ease for Ubuntu"
maintainer="Example <example@yap.org>"
arch=('amd64')
license=('GPLv3')
section=utils
priority=optional
url="https://github.com/PackageFoundation/${pkgname}"
depends=('glibc')
depends__apk=('musl')
depends__apt=('libc6')
depends__rpm=('glibc')
sources=(
  "${url}/archive/main.tar.gz"
)
hashsums=(
  "SKIP"
)

build() {
  cd "${srcdir}/${pkgname}-main"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie \
  -trimpath \
  -mod=readonly \
  -modcacherw"

  go build -ldflags="-w -s \
  -buildid='' \
  -linkmode=external \
  -X main.version=${pkgver}" \
    .
}

package() {
  cd "${srcdir}/${pkgname}-main"

  install -Dm 755 "${pkgname}" \
    "${pkgdir}/usr/bin/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"

  ./"${pkgname}" completion bash >"${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  ./"${pkgname}" completion zsh >"${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  ./"${pkgname}" completion fish >"${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"

  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}/GPL3" LICENSE
  install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.md
}
